// https://plugins.gradle.org/docs/publish-plugin
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.0"
  }
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

defaultTasks 'clean', 'build'

sourceCompatibility = targetCompatibility = 1.8

// ======== create source and javadoc bundles ========

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()

  compile 'org.codehaus.groovy:groovy-all:2.4.3'
  compile 'com.amazonaws:aws-java-sdk:1.10.2' // http://aws.amazon.com/releasenotes/Java?browse=1
}

pluginBundle {
  website = 'http://www.gradle.org/'
  vcsUrl = 'https://github.com/ikakara-team/gradle-aws-util'

  plugins {
    lambdaPlugin {
      id = 'aws.util.lambda'
      displayName = 'Gradle AWS-Lambda Plugin'
      description = 'Develop AWS Lambda functions and use this plugin to upload/manage them.'
      tags = ['AWS', 'Lambda']
      version = '0.2-SNAPSHOT'
    }
  }
  mavenCoordinates {
    groupId = "ikakara.awsutil"
    artifactId = "lambda"
    version = "0.2-SNAPSHOT"
  }
}
