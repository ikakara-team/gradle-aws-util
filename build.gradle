// https://plugins.gradle.org/docs/publish-plugin
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
  }
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

defaultTasks 'clean', 'build'

sourceCompatibility = targetCompatibility = 1.8

// ======== create source and javadoc bundles ========

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

repositories {
  jcenter()
}

dependencies {
  compile gradleApi()
  compile localGroovy()

  //compile 'org.codehaus.groovy:groovy-all:2.4.3'
  compile 'com.amazonaws:aws-java-sdk:1.10.10' // http://aws.amazon.com/releasenotes/Java?browse=1

  // tests
  testCompile "junit:junit:4.12"
  testCompile "org.hamcrest:hamcrest-library:1.3"
  testCompile "org.mockito:mockito-core:1.9.5"
}

///////////////////////////////////////////////////////////////////////////////
// For publishing plugin to gradle.plugin and mavenLocal
///////////////////////////////////////////////////////////////////////////////

// Unless overridden in the pluginBundle config DSL, the project version will
// be used as your plugin version when publishing
version = "0.1.9"
group = "ikakara.awsutil"

pluginBundle {
  website = 'https://github.com/ikakara-team/gradle-aws-util'
  vcsUrl = 'https://github.com/ikakara-team/gradle-aws-util'

  plugins {
    lambdaPlugin {
      id = 'ikakara.awsutil.lambda'
      displayName = 'Gradle AWS-Lambda Plugin'
      description = 'Develop AWS Lambda functions and use this plugin to upload/manage them.'
      tags = ['AWS', 'Lambda']
    }
  }
}

///////////////////////////////////////////////////////////////////////////////
// For local install/testing
///////////////////////////////////////////////////////////////////////////////
apply plugin: 'maven'

install {
  repositories.mavenInstaller {
    pom.version = version
    pom.groupId = "gradle.plugin.ikakara.awsutil"
  }
}

// http://mrhaki.blogspot.com/2009/11/gradle-goodness-use-our-local-maven.html
// The following line makes the install tasks depend on the build task
// and now all tests and checks are done before install is executed.
install.dependsOn ':build'
